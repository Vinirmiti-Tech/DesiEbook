apply plugin: 'com.android.application'

import com.android.build.OutputFile

android {
    compileSdkVersion 25
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.github.axet.bookreader"
        minSdkVersion 9
        targetSdkVersion 26
        versionCode 353
        versionName "1.11.25"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    signingConfigs {
        release {
            storeFile file(prop('RELEASE_STORE_FILE') ?: 'none')
            storePassword prop('RELEASE_STORE_PASSWORD')
            keyAlias prop('RELEASE_KEY_ALIAS')
            keyPassword prop('RELEASE_KEY_PASSWORD')
        }
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def abi = output.getFilter(OutputFile.ABI)
            if (abi == null)
                abi = "universal";
            output.outputFileName = org.apache.commons.io.FilenameUtils.getExtension(android.defaultConfig.applicationId) + "-" + abi + "-" + android.defaultConfig.versionName + "-" + variant.buildType.name + ".apk"
        }
    }
    bundle {
        language {
            enableSplit = false
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    implementation 'com.github.axet:android-library:1.30.5' // implementation project(':android-library')
    implementation 'com.android.support:design:25.3.1'
    implementation 'com.android.support:support-vector-drawable:25.3.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.github.axet.fbreader:library:0.1.13'
    implementation 'com.github.axet:djvulibre:0.1.1'
    implementation 'com.github.axet:pdfium:2.0.11'
    implementation 'com.github.axet:k2pdfopt:0.1.5'
    implementation('com.github.axet:wget:1.4.2') { exclude group: 'com.thoughtworks.xstream', module: 'xstream' }
}
